// Test to check that the "warning blocks" are displayed as expected.
go-to: "file://" + |DOC_PATH| + "/test_docs/struct.Foo.html"
show-text: true

define-function: (
    "check-warning",
    (theme, color, note_color, warning_color, danger_color),
    block {
        set-local-storage: {"rustdoc-theme": |theme|, "rustdoc-use-system-theme": "false"}
        reload:

        // The IDs are added directly into the DOM to make writing this test easier.
        assert-css: ("#doc-note", {
            "margin-bottom": "12px",
            "color": |color|,
            "border-left": "2px solid " + |note_color|,
            "background-color": "transparent",
        })
        assert-css: ("#doc-warning-1", {
            "margin-bottom": "12px",
            "color": |color|,
            "border-left": "2px solid " + |warning_color|,
            "background-color": "transparent",
        })
        assert-css: ("#doc-danger", {
            "margin-bottom": "12px",
            "color": |color|,
            "border-left": "2px solid " + |danger_color|,
            "background-color": "transparent",
        })
        assert-css: ("#doc-warning-2", {
            "margin-bottom": "0px",
            "color": |color|,
            "border-left": "2px solid " + |warning_color|,
            "background-color": "transparent",
        })
    },
)

call-function: ("check-warning", {
    "theme": "ayu",
    "color": "#c5c5c5",
    "note_color": "#00abff",
    "warning_color": "#ff8e00",
    "danger_color": "#ff2323",
})
call-function: ("check-warning", {
    "theme": "dark",
    "color": "#ddd",
    "note_color": "#0087ff",
    "warning_color": "#ff8e00",
    "danger_color": "#d22525",
})
call-function: ("check-warning", {
    "theme": "light",
    "color": "black",
    "note_color": "#0087ff",
    "warning_color": "#ff8e00",
    "danger_color": "#d22525",
})
